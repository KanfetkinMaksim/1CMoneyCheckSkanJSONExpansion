Функция ПолучитьМагазин(Наименование,Адрес) Экспорт
	НаименованиеИАдрес = Лев(Наименование + ", " + Адрес,80);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Контакты.Ссылка КАК Ссылка,
	               |	Контакты.Адрес КАК Адрес
								 |ПОМЕСТИТЬ КонтактыПоНаименованию
	               |ИЗ
	               |	Справочник.Контакты КАК Контакты
	               |ГДЕ
	               |	(Контакты.Наименование = &Наименование
								 | ИЛИ Контакты.Наименование = &НаименованиеИАдрес)
								 |	И Контакты.Родитель = &Родитель
								 |
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	КонтактыПоНаименованию.Ссылка КАК Ссылка
	               |ИЗ
	               |	КонтактыПоНаименованию КАК КонтактыПоНаименованию
	               |ГДЕ
	               |	(ВЫРАЗИТЬ(КонтактыПоНаименованию.Адрес КАК СТРОКА(1024))) = &Адрес
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ КонтактыПоНаименованию";
	Запрос.УстановитьПараметр("Наименование",Наименование);
	Запрос.УстановитьПараметр("НаименованиеИАдрес",НаименованиеИАдрес);
	Запрос.УстановитьПараметр("Адрес",Адрес);
	Запрос.УстановитьПараметр("Родитель",Справочники.Контакты.Магазины);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СоздатьМагазин(Наименование,Адрес);
	Иначе
		Результат = Результат.Выбрать();
		Результат.Следующий();
		Возврат Результат.Ссылка;
	КонецЕсли;
КонецФункции

Функция СоздатьМагазин(Наименование,Адрес)
	Магазин = Справочники.Контакты.СоздатьЭлемент();
	Магазин.Родитель = Справочники.Контакты.Магазины;
	Магазин.Наименование = Наименование;
	Магазин.Адрес = Адрес;
	Магазин.Активность = Истина;
	Попытка
		Магазин.Записать();
	Исключение
		Магазин.Наименование = Лев(Наименование + ", " + Адрес,80);
		Магазин.Записать();
	КонецПопытки;
	Сообщить(СтрШаблон("Создан магазин %1.",Магазин.Ссылка));
	Возврат Магазин.Ссылка;
КонецФункции