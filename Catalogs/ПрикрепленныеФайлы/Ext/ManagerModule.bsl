Функция НайтиФайл(Наименование) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПрикрепленныеФайлы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ПрикрепленныеФайлы КАК ПрикрепленныеФайлы
	               |ГДЕ
	               |	ПрикрепленныеФайлы.Наименование = &Наименование
	               |	И НЕ ПрикрепленныеФайлы.ПометкаУдаления";
	Запрос.УстановитьПараметр("Наименование",Наименование);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Результат.Выбрать();
	Результат.Следующий();
	
	Возврат Результат.Ссылка;
КонецФункции

// На основе данных файла на диске, готовит и возвращает структуру, описывающую прикрепляемый файл и пригодную 
// для использования как на сервере, так и на клиенте.
//Двоичные данные помещаются во временное хранилище либо с идентификатором формы, либо с произвольным уникатльным идентификатором
//
// Параметры:
//  ИмяФайла			 - Строка - Имя файла на диске
//  ВладелецХранилища	 - УправляемаяФорма или УникальныйИдентификатор - Форма-владелец будущего файла или уникальный идентификатор для сохранения во временном хранилище
// 
// Возвращаемое значение:
//  Структура (см. ПрикрепленныеФайлыКлиентСервер.НоваяСтруктураПрикрепляемогоФайла()) или НЕОПРЕДЕЛЕНО 
//в случае, если устройство не поддерживает мультимедиа
//
Функция СтруктураФайлаПоЕгоИмени(ИмяФайла, ВладелецХранилища = Неопределено) Экспорт

	Если (ИмяФайла = Неопределено) ИЛИ ПустаяСтрока(ИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	ТекФайл = Новый Файл(ИмяФайла);
	Если ТекФайл.ЭтоКаталог() ИЛИ (НЕ ТекФайл.Существует()) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ПрикрепленныеФайлыКлиентСервер.НоваяСтруктураПрикрепляемогоФайла();
	
	//Результат.СпособОткрытия = ПолучитьСпособОткрытияФайла(ТекФайл);
	Результат.Расширение     = ТекФайл.Расширение;
	Результат.ДатаСоздания   = ТекущаяДата();
	Результат.Размер         = ТекФайл.Размер();
	
	ИД = Неопределено; 
	Если ТипЗнч(ВладелецХранилища) = Тип("УправляемаяФорма") Тогда
		ИД = ВладелецХранилища.УникальныйИдентификатор;
	ИначеЕсли ТипЗнч(ВладелецХранилища) = Тип("УникальныйИдентификатор") Тогда
		ИД = ВладелецХранилища;
	КонецЕсли;
	
	Данные = Новый ДвоичныеДанные(ИмяФайла);
	Результат.НавигационнаяСсылка = ПоместитьВоВременноеХранилище(Данные, ИД);	
	Результат.Наименование        = ТекФайл.ИмяБезРасширения;
	Результат.Иконка              = ПрикрепленныеФайлыКлиентСервер.ИконкаПрикрепленногоФайла(Результат.СпособОткрытия);
	
	Возврат Результат;

КонецФункции